
Перем кэшРазделителиСлов;
Перем ключевыеСловаПереноса;
Перем ключевыеСловаНеКода;


#Область ПрограммныйИнтерфейс

Функция ПолучитьСтрокиМодуля( Знач Путь ) Экспорт
	
	тзСтрокиМодуля = Новый ТаблицаЗначений();
	тзСтрокиМодуля.Колонки.Добавить("Номер");
	тзСтрокиМодуля.Колонки.Добавить("Содержимое");
	
	ключевыеСловаНеКода = КлючевыеСловаНачалаНеКода();
	ключевыеСловаПереноса = КлючевыеСловаПереноса();
	кэшРазделителиСлов = Новый Соответствие();

	Чтение = Новый ЧтениеТекста();
	Чтение.Открыть(Путь, КодировкаТекста.UTF8);
	
	ТекстСтроки = Чтение.ПрочитатьСтроку();
	
	НомерСтроки = 1;
	предСтрокаЗакончиласьПереносом = Ложь;
	
	Пока НЕ ТекстСтроки = Неопределено Цикл
		
		Если ЕстьКодВСтроке(ТекстСтроки, предСтрокаЗакончиласьПереносом) Тогда
			
			новСтрока = тзСтрокиМодуля.Добавить();
			
			новСтрока.Номер = НомерСтроки;
			новСтрока.Содержимое = ТекстСтроки;
			
		КонецЕсли;
		
		предСтрока = ТекстСтроки;
		
		ТекстСтроки = Чтение.ПрочитатьСтроку();
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
	Чтение.Закрыть();
	
	Возврат тзСтрокиМодуля;

	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЕстьКодВСтроке(Знач пСтрокаКода, предСтрокаЗакончиласьПереносом)
	
	строкаКода = ВРег( СокрЛП( пСтрокаКода ) );
	
	Если НЕ ЗначениеЗаполнено(строкаКода) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если СтрНачинаетсяС( строкаКода, "//" ) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если СтрНачинаетсяС( строкаКода, "|" ) Тогда
		предСтрокаЗакончиласьПереносом = СтрокаЗаканчиваетсяПереносом( строкаКода );
		Возврат Ложь;
	КонецЕсли;
	
	Если СтрНачинаетсяС( строкаКода, "#" )
		ИЛИ СтрНачинаетсяС( строкаКода, "&" ) Тогда
		предСтрокаЗакончиласьПереносом = Ложь;
		Возврат Ложь;
	КонецЕсли; 
	
	строкаКода = КодБезКомментариев( строкаКода );
	
	Для каждого цИсключение Из ключевыеСловаНеКода Цикл
		
		Если СтрНачинаетсяС( строкаКода, ВРег( цИсключение ) )
			И ЭтоОтдельноеСлово( ВРег( цИсключение ), строкаКода, 1 ) Тогда
			предСтрокаЗакончиласьПереносом = Ложь;
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	естьКодВСтроке = Истина;
	
	Если предСтрокаЗакончиласьПереносом Тогда
		
		естьКодВСтроке = Ложь;
		
	КонецЕсли;
	
	предСтрокаЗакончиласьПереносом = СтрокаЗаканчиваетсяПереносом( строкаКода );
	
	Возврат естьКодВСтроке;
	
КонецФункции

Функция СтрокаЗаканчиваетсяПереносом( Знач пСтрока )
	
	Для каждого цСлово Из ключевыесловаПереноса Цикл
		
		Если СтрЗаканчиваетсяНа( пСтрока, цСлово ) Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция КодБезКомментариев( Знач пСтрока )
	
	ПозицияКомментария = ПозицияФрагмента( пСтрока, "//" );
	
	Если ПозицияКомментария = 0 Тогда
		
		Возврат пСтрока;
		
	Иначе
		
		Возврат Лев( пСтрока, ПозицияКомментария - 1 );
		
	КонецЕсли;
	
КонецФункции

Функция КлючевыеСловаПереноса()
	
	словаПереноса = Новый Массив;
	
	словаПереноса.Добавить( ";" );
	словаПереноса.Добавить( "ТОГДА" );
	словаПереноса.Добавить( "ИНАЧЕ" );
	словаПереноса.Добавить( "ЦИКЛ" );
	словаПереноса.Добавить( "КОНЕЦПРОЦЕДУРЫ" );
	словаПереноса.Добавить( "КОНЕЦФУНКЦИИ" );
	словаПереноса.Добавить( "ЭКСПОРТ" );
	
	Возврат словаПереноса;
	
КонецФункции

Функция КлючевыеСловаНачалаНеКода()

	ключевыеСлова = Новый Массив;
	ключевыеСлова.Добавить( "Процедура" );
	ключевыеСлова.Добавить( "КонецПроцедуры" );
	ключевыеСлова.Добавить( "Функция" );
	ключевыеСлова.Добавить( "КонецФункции" );
	ключевыеСлова.Добавить( "Иначе" );
	ключевыеСлова.Добавить( "КонецЕсли" );
	ключевыеСлова.Добавить( "КонецЦикла" );
	ключевыеСлова.Добавить( "Попытка" );
	ключевыеСлова.Добавить( "Исключение" );
	ключевыеСлова.Добавить( "КонецПопытки" );
	
	Возврат ключевыеСлова;

КонецФункции

Функция ПозицияФрагмента( Знач пСтрока, Знач пФрагмент )
	
	позицияФрагмента = СтрНайти( пСтрока, пФрагмент );
	
	Если позицияФрагмента = 0 Тогда
		
		Возврат 0;
		
	КонецЕсли;
	
	Если Не пФрагмент = "//" Тогда
		
		ПозицияКомментария = ПозицияФрагмента( пСтрока, "//" );
		
		Если ПозицияКомментария > 0
			И ПозицияКомментария < позицияФрагмента Тогда
			
			Возврат 0;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПозицияКавычек = СтрНайти( пСтрока, """" );
	
	// Проверяем есть ли вначале строки символ "|".
	ПервыйСимвол = Сред( СокрЛП( пСтрока ), 1, 1 );
	
	Если пФрагмент = "|"
		И ПервыйСимвол = "|" Тогда
		
		Возврат позицияФрагмента;
		
	КонецЕсли;
	
	// Нет кавычек, оставляем весь комментарий.
	// Кавычки после комментария, оставляем весь комментарий.
	
	Если ( ПозицияКавычек = 0 ИЛИ ПозицияКавычек > позицияФрагмента )
		И ПервыйСимвол <> "|" Тогда
		
		Возврат позицияФрагмента;
		
	ИначеЕсли ПервыйСимвол = "|"
		И ПозицияКавычек = 0 Тогда
		
		// Все в строке
		
		Возврат 0;
		
	ИначеЕсли ПервыйСимвол = "|"
		И ПозицияКавычек > 0 Тогда
		
		строкаПослеКавычек = Сред( пСтрока, ПозицияКавычек + 1 );
		
		позицияФрагмента = ПозицияФрагмента( строкаПослеКавычек, пФрагмент );
		
		Если позицияФрагмента = 0 Тогда
			
			Возврат 0;
			
		Иначе
			
			Возврат ПозицияКавычек + позицияФрагмента;
			
		КонецЕсли;
		
	Иначе
		
		строкаПослеКавычек = "|" + Сред( пСтрока, ПозицияКавычек + 1 );
		
		позицияФрагмента = ПозицияФрагмента( строкаПослеКавычек, пФрагмент );
		
		Если позицияФрагмента = 0 Тогда
			
			Возврат 0;
			
		Иначе
			
			Возврат ПозицияКавычек + позицияФрагмента - 1;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

Функция ЭтоОтдельноеСлово( Знач пСлово, Знач пСтрока, Знач пПозиция )
	
	слеваРазделитель  = Ложь;
	справаРазделитель = Ложь;
	
	Если пПозиция = 1 Тогда
		
		слеваРазделитель = Истина;
		
	Иначе
		
		слеваРазделитель = ЭтоРазделительСлов( КодСимвола( пСтрока, пПозиция - 1 ) );
		
	КонецЕсли;
	
	позицияПравогоРазделителя = пПозиция + СтрДлина( пСлово );
	
	Если позицияПравогоРазделителя > СтрДлина( пСтрока ) Тогда
		
		справаРазделитель = Истина;
		
	Иначе
		
		справаРазделитель = ЭтоРазделительСлов( КодСимвола( пСтрока, позицияПравогоРазделителя ) );
		
	КонецЕсли;
	
	Возврат слеваРазделитель И справаРазделитель;
	
КонецФункции

Функция ЭтоРазделительСлов( Знач КодСимвола )
	
	значениеКеша = кэшРазделителиСлов[ КодСимвола ];
	
	Если Не значениеКеша = Неопределено Тогда
		
		Возврат значениеКеша;
		
	КонецЕсли;
	
	Диапазоны = Новый Массив;
	Диапазоны.Добавить( Новый Структура( "Мин,Макс", 48, 57 ) );     // цифры
	Диапазоны.Добавить( Новый Структура( "Мин,Макс", 65, 90 ) );     // латиница большие
	Диапазоны.Добавить( Новый Структура( "Мин,Макс", 97, 122 ) );    // латиница маленькие
	Диапазоны.Добавить( Новый Структура( "Мин,Макс", 1040, 1103 ) ); // кириллица
	Диапазоны.Добавить( Новый Структура( "Мин,Макс", 1025, 1025 ) ); // символ "Ё"
	Диапазоны.Добавить( Новый Структура( "Мин,Макс", 1105, 1105 ) ); // символ "ё"
	Диапазоны.Добавить( Новый Структура( "Мин,Макс", 95, 95 ) );     // символ "_"
	
	Для Каждого Диапазон Из Диапазоны Цикл
		
		Если КодСимвола >= Диапазон.Мин И КодСимвола <= Диапазон.Макс Тогда
			
			кэшРазделителиСлов.Вставить( КодСимвола, Ложь );
			
			Возврат Ложь;
			
		КонецЕсли;
		
	КонецЦикла;
	
	кэшРазделителиСлов.Вставить( КодСимвола, Истина );
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти



